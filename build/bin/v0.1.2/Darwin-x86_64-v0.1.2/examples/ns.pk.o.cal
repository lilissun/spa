@string true;

@function (@variable skey) pk(skey);
@function (@variable mess, @variable skey) sign(mess, skey);
@function (@variable mess, @variable skey) check(sign(mess, skey), pk(skey)) -> true;
@function (@variable mess, @variable skey) get(sign(mess, skey)) -> mess;
@function (@variable mess, @variable pkey) aenc(mess, pkey);
@function (@variable mess, @variable skey) adec(aenc(mess, pk(skey)), skey) -> mess;

@string SKA;
@string SKB;

@init init;
@join join;
@accept accept;

@process Pa(@variable skA, @variable pkB) {
    @receive @variable pkR;
    @new @nonce na;
    @send aenc((na, pk(skA)), pkR);
    @receive @variable m;
    @let (== na, @variable nb) = adec(m, skA) @then
    @read @timestamp ta;
    @engage init(pk(skA), pkR, na, nb)@ta;
    @send aenc(nb, pkR);
}

@process Pb(@variable skB, @variable pkA) {
    @receive @variable m;
    @let (@variable na, @variable pkI) = adec(m, skB) @then
    @new @nonce nb;
    @send aenc((na, nb), pkI);
    @receive @variable m1;
    @let ==nb = adec(m1, skB) @then
    @if (pkI == pkA) @then
    @read @timestamp tb;
    @engage accept(pkI, pk(skB), na, nb)@tb;
}

@process Pp(@variable pkB, @variable pkA) {
    @send pkA;
    @send pkB;
}

@system {
    @parallel { 
        !{ @invoke Pa(SKA, pk(SKB)); } | 
        !{ @invoke Pb(SKB, pk(SKA)); } | 
        !{ @invoke Pp(pk(SKA), pk(SKB)); } 
    }
}

@correspondence (@variable pkA, @variable pkB, @variable na, @variable nb, @timestamp ta, @timestamp tb) {
    @enforced;
    @event init(pkA, pkB, na, nb)@ta;
    @event accept(pkA, pkB, na, nb)@tb;
    @constraint ta <= tb;
}